; Script generated by Inno Setup Script Wizard.
; Modified for Calvindustries CopilotRemap.

#define AppName "CopilotRemap"
#define AppVer "1.0"
#define AppPub "Calvindustries"
#define AppExe "CopilotRemap.exe"
#define AppIco "CopilotRemap.ico"

[Setup]
AppId={{4AE1E209-E610-42E3-AD81-1E57C4896FC2}
AppName={#AppName}
AppVerName={#AppName}
AppVersion={#AppVer}
AppPublisher={#AppPub}
DefaultDirName={autopf}\{#AppPub}\{#AppName}
DefaultGroupName={#AppPub}\{#AppName}
UninstallDisplayIcon={app}\{#AppIco}
ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible
DisableProgramGroupPage=yes
PrivilegesRequired=admin
OutputBaseFilename=CopilotRemapInstaller
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: ".\{#AppExe}"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\icon\{#AppIco}"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{group}\{#AppName}"; Filename: "{app}\{#AppExe}"; IconFilename: "{app}\{#AppIco}"

[Run]
Filename: "{sys}\schtasks.exe"; \
    Parameters: "/Create /TN ""{#AppName}"" /TR ""'{app}\{#AppExe}'"" /SC ONLOGON /RL HIGHEST /F"; \
    Flags: runhidden

Filename: "{app}\{#AppExe}"; \
    Description: "Enable CopilotRemap immediately"; \
    Flags: postinstall nowait runhidden
    
[UninstallRun]
Filename: "{sys}\taskkill.exe"; \
    Parameters: "/F /IM {#AppExe} /T"; \
    Flags: runhidden; RunOnceId: "StopCopilotApp"

Filename: "{sys}\schtasks.exe"; \
    Parameters: "/Delete /TN ""{#AppName}"" /F"; \
    Flags: runhidden; RunOnceId: "RemoveCopilotTask"